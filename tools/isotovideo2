#!/usr/bin/perl -w
# allocate one test dir from a pool and run isotovideo3
use strict;
use Time::HiRes qw(sleep);
use Fcntl qw(:DEFAULT :flock);

my @dirs=<pool/?>;
my $lockname="kvm2.lock";
my $allocname="queuesched.pid";
#print "@dirs\n";

sleep(rand(2)); # reduce chance of collisions with make -j

# lock+select testdir
my $found=0;
if($ENV{testdir} ne "testrun-cron") {@dirs=($ENV{testdir})}
foreach my $d (@dirs) {
	my $l="$d/$lockname";
	next if -e $l; # occopied for sure
	# likely free, use own allocation lock protocol
	# tmp will be autocleaned, so we can just exec
	mkdir "$d/tmp";
	my $a="$d/tmp/$allocname";
	sysopen my $fh, $a, O_RDWR|O_CREAT or die "cant create $a";
	flock $fh, LOCK_EX or next;
	my $num = <$fh>;
	if($num && $num=~m/^\d{1,5}$/) {
		my $signalled=kill(0, $num);
		if($signalled) {
			print "someone else owns $a and is alive\n";
			next;
		}
	}
	# process is dead => cleanup
	seek $fh, 0, 0 or die "can't rewind numfile: $!";
	truncate $fh, 0 or die "can't truncate numfile: $!";
	print $fh "$$\n";
	close($fh);
	$ENV{testdir}=$d;
	$found=1;
	last;
}
if(!$found) {die "all dirs (@dirs) are currently in use"}
print "using testdir=$ENV{testdir}\n";
exec("withlock", "$ENV{testdir}/$lockname", "tools/isotovideo3");
die "exec failed: $!";
