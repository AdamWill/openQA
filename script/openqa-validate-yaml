#!/usr/bin/env perl
# Copyright (C) 2020 SUSE LLC
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

use strict;
use warnings;
use 5.010;

use FindBin '$Bin';
use lib "$Bin/../lib";
use Getopt::Long::Descriptive;
use OpenQA::JobTemplates 'validate_data';
use OpenQA::JobTemplates 'load_yaml';

my ($opt, $usage) = describe_options(
    <<"EOM",
$0 %o <yaml-file>

Validate a YAML file against a schema.
You can pass a YAML file or '-' for STDIN.
EOM
    ['validate-schema', 'Validate Schema file itself also'],
    ['schema-file=s',   "Schema file to validate against (default public/schema/JobTemplates-01.yaml)"],
    ['help',            'print usage message and exit', {shortcircuit => 1}],
);

print($usage->text), exit if $opt->help;

my ($file) = @ARGV;

my $default_schema = "$Bin/../public/schema/JobTemplates-01.yaml";
my $schema         = $opt->schema_file // $default_schema;
unless (-f $schema) {
    say "Schema file '$schema' does not exist";
    exit 1;
}
my $data;

unless ($file) {
    say "Nothing to do";
    exit;
}
if ($file eq '-') {
    my $yaml = do { local $/; <STDIN> };
    $data = load_yaml(string => $yaml);
}
else {
    $data = load_yaml(file => $file);
}

if ($opt->validate_schema) {
    say '# validating schema, too';
}
my $errors = validate_data(
    schema_file     => $schema,
    data            => $data,
    validate_schema => $opt->validate_schema,
);

if (@$errors) {
    say $_ for @$errors;
    exit 1;
}
else {
    say "Validated!";
}
