#!/usr/bin/perl -w
use strict;
use CGI ":standard";
use FindBin;
use lib "$FindBin::Bin/modules";
use awstandard;
use sort_list;
use openqa;
use Scheduler ();
use Text::MicroTemplate "encoded_string";
use Text::MicroTemplate::Extended;

my $defaulthoursfresh=4*24;

my %options;
for my $p (qw'sort hours ib match ob') {
	$options{$p}=param($p);
}
if($options{ib}) {
	require boring;
}
if($options{match}) {
	$options{match}=~s/[^\w\[\]\{\}\(\),:.+*?\\\$^|-]//g; # sanitize
}

if(!defined $options{sort}){$options{sort}="-mtime";} # default sort by Time descending
if(!defined($options{hours}) || $options{hours}=~m/\D/) {$options{hours}=$defaulthoursfresh;}
my $hoursfresh=($options{hours}+0);
if($hoursfresh<1 || $hoursfresh>900) {$hoursfresh=$defaulthoursfresh}
my $maxage=3600*$hoursfresh;

my $maxmtime=(stat($ENV{SCRIPT_FILENAME}))[9];

# schedule list
my @slist=();
for my $job (@{Scheduler::list_jobs('state' => 'scheduled')||[]}) {
	my $testname = $job->{'name'};
	next if($options{match} && $testname !~ m/$options{match}/);
	my $params = openqa::parse_testname($testname);
	push(@slist, {
		priority => $job->{priority},
		testname=>$testname, distri=>$params->{distri}.'-'.$params->{version}, type=>$params->{flavor}, arch=>$params->{arch},
		build=>$params->{build}, extrainfo=>$params->{extrainfo}, mtime=>0
		}
	);

}
# schedule list end

my @list=();
my $now=time();
for my $r (<$resultdir/*>) {
	next unless -d $r;
	my @s;
	if(-e "$r/autoinst-log.txt") {@s = stat("$r/autoinst-log.txt");}
	else {@s = stat($r);} # running test don't have a logfile, yet
	my $mtime = $s[9];
	if($mtime>$maxmtime){$maxmtime=$mtime}
	next if $mtime < $now - $maxage; # skip old
	next if($options{match} && $r!~m/$options{match}/);
	my $testname = path_to_testname($r);
	my $params = openqa::parse_testname($testname);

	my $run_stat = {};

	my $results = test_result($testname);

	my $result_stats = test_result_stats($results);
	my $result = test_result_hash($results);

	my $running = 0;
	if(not -e "$r/autoinst-log.txt") {
		# running
		my $running_basepath = running_log($testname);
		$run_stat = get_running_modinfo($results);
		$run_stat->{'run_backend'} = 0;
		if(-e "$running_basepath/os-autoinst.pid") {
			my $backpid = file_content("$running_basepath/os-autoinst.pid");
			chomp($backpid);
			$run_stat->{'run_backend'} = (-e "/proc/$backpid"); # kill 0 does not work with www user
		}
		$running = 1;
	} else {
		$mtime = (stat(_))[9];
	}
	my $backend = $results->{'backend'}->{'backend'} || '';
	$backend =~s/^.*:://;
	if($options{ib}) {
		next if(boring::is_boring($r, $result));
	}
	if($options{ob}) {
		next if($options{ob} ne "" and $options{ob} ne $backend);
	}
	push(@list, {
		testname=>$testname, running=>$running, distri=>$params->{distri}, type=>$params->{flavor}, arch=>$params->{arch},
		build=>$params->{build}, extrainfo=>$params->{extrainfo}, mtime=>$mtime, backend => $backend,
		res_ok=>$result_stats->{ok}||0, res_unknown=>$result_stats->{unk}||0, res_fail=>$result_stats->{fail}||0,
		res_overall=>$results->{overall}, res_dents=>$results->{dents}, run_stat=>$run_stat
	});
}

my @sorted_slist = sort_list(
	{build=>\&sort_num, mtime=>\&sort_num},
	sort_param_to_keys($options{sort}), \@slist
);

my @sorted_list = sort_list(
	{running=>\&sort_num, build=>\&sort_num, mtime=>\&sort_num, res_ok=>\&sort_num, res_unknown=>\&sort_num, res_fail=>\&sort_num},
	sort_param_to_keys($options{sort}), \@list
);

my $mt = Text::MicroTemplate::Extended->new(
	include_path  => ['../htdocs/template'],
	template_args => { schedulelist => \@sorted_slist, resultlist => \@sorted_list, options => \%options, hoursfresh=>$hoursfresh },
	macro => { sortarrows => sub { encoded_string(sortkeys(\%options, $_[0])); } },
);

foreach my $rundir (@runner) {
	my @s = stat($rundir.'/currentstep') || next;
	my $mtime = $s[9] || 0;
	if($mtime>$maxmtime){$maxmtime=$mtime}
}

#print header(-charset=>"UTF-8", -type=>"text/html", -Last_Modified=>awstandard::HTTPdate($maxmtime));
print header(-charset=>"UTF-8", -type=>"text/html");
print $mt->render('currentresults');
