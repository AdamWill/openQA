#!/usr/bin/perl -w
use strict;
use CGI ":standard";
use FindBin;
use lib "$FindBin::Bin/modules";
use openqa;


my $up=param("upload");
my $testname=param("testname");

my $ip=$ENV{REMOTE_ADDR};
my %whitelist=(
	"127.0.0.1"=>1, # occurs for 10.0.2.2
);
if(!$whitelist{$ip}) {
	print header(-status=>403)."forbidden";
	exit 2;
}

$testname = sanitize_testname($testname);
my $upname = $up;
$upname =~ s#.*/##;
$upname = sanitize_testname($upname);
unless ($upname && $testname) {
	$testname ||= '';
	print header(-status=>"400 invalid parmeters");
	warn "invalid parameters passed, testname '$testname', upname '$upname'\n";
	exit 1;
}
unless (-l join('/', $resultdir, $testname)) {
	print header(-status=>"404 test not running");
	warn "test $testname is no running, refused to upload logs";
	exit 1;
}
print STDERR "$upname $testname\n";
my $dir = join('/', $loguploaddir, $testname);
mkdir($dir);
my $file = join('/', $dir, $upname);
if (open(my $out, ">", $file)) {
	while(<$up>) {
		print $out $_;
	}
} else {
	print header(-status=>500);
	warn "opening $upname failed: $!\n";
	exit 1;
}

print header(),"OK: $testname -> $upname\n";
