#!/usr/bin/perl -w
use strict;
use CGI ":standard";
use Image::Magick;
use lib "/srv/www/cgi-bin/modules";
use awstandard;
use openqa;

my $p = new Image::Magick(depth=>8);
my $name=$ENV{PATH_INFO};
if($name=~m/\.\./) {print header(-status=>403)."invalid path"; exit 0; }
$name=~s/\.ppm$//;
my $fullname="$basedir/opensuse/$name.ppm";
if (!-e $fullname) {
	$fullname.=".gz";
	if (!-e $fullname) {print header(-status=>404)."file not found"; exit 0; }
}
$p->Read($fullname, depth=>8);

my $size=param("size");
if($size && $size=~m/^\d{1,3}x\d{1,3}$/) {
	$p->Resize($size); # make thumbnail
}

my $csize=param("csize");
if($csize && $csize=~m/^\d{1,3}x\d{1,3}$/) {
  my ($img_width,$img_height) = $p->Get('width','height');
  my ($width,$height) = split('x',$csize);
  my $ratio = $width / $height;
  my ($x,$y);
  if($ratio >= $img_width / $img_height) {
    $x = $img_width;
    $y = $img_width / $ratio;
  }
  else {
    $x = $img_height * $ratio;
    $y = $img_height;
  }
  $p->Crop($x.'x'.$y.'+0+0');
	$p->Resize($csize); # make thumbnail
}

my $mtime=(stat($fullname))[9];
my $format="jpg";
if($ENV{REQUEST_URI}=~m/\.(\w{1,4})$/) {$format=$1}

print header(-type=>"image/$format", -expires=>'+30m', -max_age=>'20000', -Last_Modified=>awstandard::HTTPdate($mtime)).
      $p->ImageToBlob(magick=>uc($format), depth=>8, quality=>80);

