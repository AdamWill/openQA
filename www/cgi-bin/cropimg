#!/usr/bin/perl -w
use strict;
use CGI qw(:standard :cgi-lib);
use FindBin;
use lib "$FindBin::Bin/modules";
use openqa;
use File::Copy;
use Text::MicroTemplate "encoded_string";
use Text::MicroTemplate::Extended;

my $fn=$ENV{PATH_INFO};
if($fn=~m/\.\./) {print header(-status=>403)."invalid path"; exit 0; }
$fn=~s%^/%%;
my($action,$testname,$testmodule,$testindex) = split(/\//, $fn);

my $results = test_result($testname);
unless ($results) { print header(-status=>404)."no testresults found"; exit 0; }

my $module = test_result_module($results->{'testmodules'}, $testmodule);
unless ($module) { print header(-status=>404)."module not found"; exit 0; }

unless ($testindex =~ m/^\d+$/ && $testindex > 0 && $testindex <= @{$module->{'details'}}) {
	print header(-status=>403)."invalid index"; exit 0;
}

my $module_detail = $module->{'details'}->[$testindex-1];
my $imgname = $module_detail->{'screenshot'};
my $modinfo = get_running_modinfo($results);

# Each object in $needles will contain the name, the url of the image and 2 lists of
# areas: 'area' and 'matches'. The former refers to the original definitions and the
# later shows the position found (best try) in the actual screenshot.
# The first element of the needles array is the screenshot itself, with an empty
# 'areas' (there is no needle associated to the screenshot) and with all matching
# areas in 'matches'.
my $needles = [];
if ($module_detail->{'needle'}) {

	# First position: the screenshot with all the matching areas (in result)
	push(@$needles, {'name' => 'screenshot', 'image' => "/$prj/testresults/$testname/$imgname",
			'area' => [], 'matches' => []});
	for my $area (@{$module_detail->{'area'}}) {
		push(@{$needles->[0]->{'matches'}},
			{'xpos' => $area->{'x'}, 'width' => $area->{'w'},
			 'ypos' => $area->{'y'}, 'height' => $area->{'h'},
			 'type' => 'match'});
	}
	# Second position: the only needle (with the same matches)
	my $needle = needle_info($module_detail->{'needle'}, $results->{'needledir'});
	push(@$needles, {'name' => $module_detail->{'needle'}, 'image' => $needle->{'imageurl'},
			'area' => $needle->{'area'}, 'matches' => $needles->[0]->{'matches'}});

} elsif ($module_detail->{'needles'}) {

	# First position: the screenshot
	push(@$needles, {'name' => 'screenshot', 'image' => "/$prj/testresults/$testname/$imgname",
			'area' => [], 'matches' => []});
	# Afterwards, all the candidate needles
	my $needleinfo;
	my $needlename;
	my $area;
	# For each candidate we will use theee variables:
	# $needle: needle information from result, in which 'areas' refers to the best matches
	# $needlename: read from the above
	# $needleinfo: actual definition of the needle, with the original areas
	# We also use $area for transforming the match information intro a real area
	for my $needle (@{$module_detail->{'needles'}}) {
		$needlename = $needle->{'name'};
		$needleinfo  = needle_info($needlename, $results->{'needledir'});
		push(@$needles, {'name' => $needlename, 'image' => $needleinfo->{'imageurl'},
			'area' => $needleinfo->{'area'}, 'matches' => []});
		for my $match (@{$needle->{'area'}}) {
			$area = {'xpos' => $match->{'x'}, 'width' => $match->{'w'},
				 'ypos' => $match->{'y'}, 'height' => $match->{'h'},
				 'type' => 'match'};
			push(@{$needles->[0]->{'matches'}}, $area);
			push(@{$needles->[scalar(@$needles)-1]->{'matches'}}, $area);
		}
	}
} else {
	# Can happen?
	push(@$needles, {'name' => 'screenshot', 'image' => "/$prj/testresults/$testname/$imgname",
			'area' => [], 'matches' => []});
}

my $mt = Text::MicroTemplate::Extended->new(
	include_path  => ['../htdocs/template'],
	template_args => { testname => $testname, testmodule => $testmodule, imgname => $imgname,
                           needles => $needles, imglist => $module->{'details'}, imgindex => $testindex,
                           modinfo => $modinfo, action => 'cropimg' });

print header(-charset=>"UTF-8", -type=>"text/html");
print $mt->render('cropimg');
